!!ARBvp1.0
OPTION ARB_position_invariant;

# input:
#-----------------------------------------
# texcoord[0]	= TEX0 texcoords
#
# local[0]		= scroll
# local[1]		= deform magnitude (1.0 is reasonable, 2.0 is twice as wavy, 0.5 is half as wavy, etc)
#
# output:
#-----------------------------------------
# texture 0		= _currentRender
# texture 1		= normal map that we will use to deform texture 0
#
# texCoord[0]	= the model surface texture coords unmodified for the mask
# texCoord[1]	= the model surface texture coords with a scroll
# texCoord[2]	= the copied deform magnitude

PARAM	scroll	= program.local[0];
PARAM	scale	= program.local[1];

PARAM	vec		= { 1.0, 0.0, 0.0, 1.0 };

TEMP	R0, R1;


# texture 0 takes the texture coordinates unmodified
MOV		result.texcoord[0], vertex.texcoord[0];

# texture 1 takes the texture coordinates and adds a scroll
ADD		result.texcoord[1], vertex.texcoord[0], scroll;

# texture 2 takes the deform magnitude and scales it by the projection distance
MOV		R0, vec;
DP4		R0.z, vertex.position, state.matrix.modelview.row[2];
DP4		R1.x, R0, state.matrix.projection.row[0];
DP4		R1.y, R0, state.matrix.projection.row[3];

# don't let the recip get near zero for polygons that cross the view plane
MAX		R1.y, R1.y, 1.0;
RCP		R1.y, R1.y;
MUL		R1.x, R1.x, R1.y;

# clamp the distance so the the deformations don't get too wacky near the view
MIN		R1.x, R1.x, 0.02;

MUL		result.texcoord[2], R1.x, scale;

END

#=====================================================================================

!!ARBfp1.0
OPTION ARB_precision_hint_fastest;

# texture 0 = _currentRender
# texture 1 = normal map that we will use to deform texture 0
# texture 2 = mask texture
#
# env[0] 	=  1.0 to _currentRender conversion
# env[1] 	=  fragment.position to 0.0 - 1.0 conversion

OUTPUT 	oColor 		= result.color;

ATTRIB	fPos		= fragment.position;
ATTRIB 	defaultTC 	= fragment.texcoord[0];
ATTRIB 	deformTC 	= fragment.texcoord[1];
ATTRIB 	scale 		= fragment.texcoord[2];

PARAM 	nonPoT		= program.env[0];
PARAM 	invRes		= program.env[1];

PARAM	const		= { 1.0, 2.0, 0.01, 0.0 };
PARAM	cdParm		= { 0.9, 0.9, 1.1, 1.1 };

TEMP	uv, invres, normalVec, mask, R0, R1, R2, R3;


# calculate the non-power-of-two inverse resolution
MUL 	invres, invRes.xyxy, nonPoT.xyxy;

# calculate the screen texcoord in the 0.0 to 1.0 range
MUL 	uv, fPos.xyxy, invres;

# load the distortion map
TEX		mask, defaultTC, texture[2], 2D;

# kill the pixel if the distortion wound up being very small
ADD		mask.xy, mask, -const.z;
KIL		mask;

# load the filtered normal map and convert to -1.0 to 1.0 range
TEX		normalVec, deformTC, texture[1], 2D;
MAD		normalVec.xyz, normalVec, const.y, -const.x;

# modulate by mask
MUL		normalVec, normalVec, mask;

# offset by the scaled normalVec and clamp it to 0.0 - 1.0
MUL 	R1, normalVec.xyxy, cdParm;
MAD_SAT	R1, R1, scale, uv;
MAD_SAT	R2, normalVec, scale, uv;
MIN 	R1, R1, nonPoT.xyxy;
MIN 	R2, R2, nonPoT.xyxy;

# sample the RGB values along the displacements
TEX		oColor.x, R1.xyxy, texture[0], 2D;
TEX		oColor.y, R2.xyxy, texture[0], 2D;
TEX		oColor.z, R1.zwzw, texture[0], 2D;

END